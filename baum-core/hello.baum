add = prim "add"
print = prim "print"
concat = prim "concat"
readline = prim "readline"
void = prim "void"
fork = prim "fork"
sleep' = prim "sleep"
sleep = |n k| sleep' n (k 0)
is0 = prim "is0"
decr = prim "decr"
store = prim "store"
read = prim "read"
write = prim "write"

putln = |s k| print (concat s "\n") k
println = |s k| print s |_| print "\n" k

fix = |f| (|x| f (|y| (x x) y)) (|x| f (|y| (x x) y))

f = fix |self| |_|
  print "input: " |_|
  readline |input|
  print "hello, " |_|
  print (concat input "!\n") |_|
  self 0

main1 = f 0

callCC = |p k| p (|x _| k x)

main2 =
  println "A" |_|
  callCC (|exit|
    println "B" |_|
    exit 5 |_|
    println "C" |_|
    void
  ) |x|
  println "D" |_|
  println x |_|
  void

p0 =
  putln "0: A" |_|
  sleep 1000 |_|
  putln "0: B" |_|
  sleep 1000 |_|
  putln "0: C" |_|
  void

p1 =
  sleep 500 |_|
  putln "1: A" |_|
  sleep 1000 |_|
  putln "1: B" |_|
  sleep 1000 |_|
  putln "1: C" |_|
  void

main3 = fork p0 p1

pair = |x y| |k| k x y
fst = |p| p (|x y| x)
snd = |p| p (|x y| y)

nat = fix |self| |n|
  is0 n (|f x| x) (|f x| let p = self (decr n) in f (p f x))

main4 = println ((nat 5) decr 13) |_| void

main5 =
  store 4 |box|
  read box |a0|
  println a0 |_|
  write box 5 |_|
  read box |a1|
  println a1 |_|
  void
  
left = |x| |l r| l x
right = |y| |l r| r y

todo = |_| _

chan = |k|
  store (left 0) |box|
  k box

send = |c x k|
  read c |s|
  s (|_| write c (right (left x)) k) (|rc| rc todo (|k2| fork (k2 x) (k 0)))

receive = |c k|
  read c |s|
  s (|_| write c (right (right k)) |_| void) (|se| se (|x| k x) todo)

sender = |c|
  sleep 100 |_|
  putln "Sender" |_|
  send c 5 |_|
  putln "Sent!" |_|
  void

receiver = |c|
  sleep 200 |_|
  putln "Receiver" |_|
  receive c |x|
  putln "Received!" |_|
  println x |_|
  void

main6 = chan |c| fork (receiver c) (sender c)



repeater = fix |self| |s|
  putln s |_|
  sleep 1000 |_|
  self s

main7 =
  fork (repeater "test") (
  sleep 1000 |_|
  putln "wait for input..." |_|
  readline "Input: " |i|
  putln (concat (concat "Hello, " i) "!") |_|
  void)

main = main7